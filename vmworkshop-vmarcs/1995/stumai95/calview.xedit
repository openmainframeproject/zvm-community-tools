/*                                                                   */
/* CALVIEW XEDIT                                                     */
/*                                                                   */
/* Usage:  This file is to be used by either invoking the DOPANEL    */
/*         command and supplying the command with the name of this   */
/*         panel or invoking a XEDIT session on a file with this     */
/*         file specified as the PROFILE to be used.                 */
/*                                                                   */
/* CALVIEW is a XEDIT file containing various XEDIT and REXX         */
/* instructions combined to construct an input panel and the         */
/* instructions necessary to process the input gathered from the     */
/* customer.  See the USAGE statement above for information on how   */
/* to invoke the CALVIEW panel.                                      */
/*                                                                   */
/* Exit Code Definitions:                                            */
/*   Not applicable.                                                 */
/*                                                                   */
/* Mod Date  Init  Description                                       */
/* --------  ----  ------------------------------------------------- */
/* 06291994  PDL   Initial development.                              */
/* 02011995  PDL   Added message handler (adopted from other panels).*/
/* 02011995  PDL   Added code to un-RESERVE XEDIT lines.             */
/* 02021995  PDL   Added trunc() functions to numeric information    */
/*                 passed to the CALVIEWV program.  This was done    */
/*                 to discard zeros prefixing the responses.         */
/* 02021995  PDL   Corrected cursor positioning after posting error  */
/*                 messages.                                         */
/* 02031995  PDL   Added code to transmit and receive responses      */
/*                 from the calendar server.                         */
/* 02031995  PDL   Modified record submitted to STUDENTS calendar    */
/*                 service machine.  The OFFICE APPOINTM GETCAL      */
/*                 command can not take a 4 digit year (e.g. 1995).  */
/*                 Now only transmit last two digits (e.g. 95).      */
/* 02081995  PDL   Yanked out code maintaining restriction that year */
/*                 be greater than or equal to 1994.                 */
/* 02091995  PDL   Reduced all displays of full years (e.g., 1995)   */
/*                 to displays of the last two digits of the year    */
/*                 (e.g., 95).  This affects messages, the customer  */
/*                 input display, and input checking code.           */
/* 02141995  BAJ   Changed title from VIEW FACULTY CALENDARS and     */
/*                 modified the instructions on the screen.          */
/* 05/31/95  BAJ   Replaced 'BETA' with 'LEVEL' in the screen title. */
/*                                                                   */
 
level="108"
 
trace off
 
/* Define the control characters for panel definition */
address xedit
"SET CTLCHAR  % ESCAPE"
"SET CTLCHAR  @ PROTECT   HIGH"
"SET CTLCHAR  ! PROTECT   NOHIGH"
"SET CTLCHAR  $ NOPROTECT HIGH"
 
/* Set up the XEDIT environment for panel definition */
"SET CASE     MIXED"
"SET LINEND   OFF"
"SET TOFEOF   OFF"
"SET PREFIX   OFF"
"SET SCALE    OFF"
"SET MSGLINE  ON 21 2"
"SET CMDLINE  OFF"
"SET MSGMODE  ON"
"VERIFY OFF 1 80"
 
/* Ensure the PF keys are defined as we need them so */
/* that we can avoid the problem with a key defined  */
/* as TABKEY, COPYKEY, NULLKEY, or CP BRKKEY (which  */
/* will cause a XEDIT READ to -NOT- terminate).  We  */
/* cannot have this.                                 */
do index=1 to 24
    "SET PF"index "BEFORE EMSG INTERNAL FAILURE:  EXIT",
    "IMMEDIATELY AND REPORT THIS TO THE HELPDESK (7997)"
end
 
/* Make sure that we set the appropriate PF */
/* keys with a QQUIT definition.  This is to*/
/* make sure the customer can get out even  */
/* though an internal failure has occurred. */
"SET PF3  BEFORE QQUIT"
"SET PF12 BEFORE QQUIT"
"SET PF15 BEFORE QQUIT"
"SET PF24 BEFORE QQUIT"
 
/* "un-RESERVE" all of the XEDIT lines */
/* that were reserved, if any.         */
"EXTRACT /RESERVED */"
do index=1 to RESERVED.0
  parse var RESERVED.index linenum otherstuff
  "SET RESERVED" linenum "OFF"
end
 
address command
 
/* Request that the panel be constructed, and place the cursor */
/* at the first input field.                                   */
call BuildDisplay
address xedit "CURSOR SCREEN 8 40"
 
/* Initialize all input variables */
currdate=date("STANDARD")
month=substr(currdate,5,2)
day=right(currdate,2)
year=substr(currdate,3,2)
userid=""
consdays="01"
 
/* Begin processing of the panel.  The "status" variable indicates */
/* what mode we are in.                                            */
/*     Status=0    indicates that the customer has submitted the   */
/*                 panel for processing.  Syntax/Field checking    */
/*                 is performed as required, and if no problem is  */
/*                 found then the external command is              */
/*                 constructed and executed.                       */
/*     Status=1    indicates that reading of the panel is to       */
/*                 continue until otherwise indicated.             */
/*     Status=2    indicates that an exit from the panel has been  */
/*                 requested.                                      */
/*     Status=3    indicates that help has been requested.         */
status=1
do while (status = 1)
 
    /* Request that the panel be rebuilt to reflect customer entries */
    call BuildDisplay
    /* Request that XEDIT read the panel and return the results */
    address xedit "READ NOCHANGE NUMBER TAG"
 
    /* Process the results of the XEDIT READ until no more are left */
    do queued()
        parse pull tag line column data
 
        /* If the result was from an input field, then process it */
        if tag = "RES" then do
            /* Remove all blanks and underscore characters */
            /* from the input                              */
            data=strip(data,trailing,"_")
            data=strip(data)
            upper data
            select
                /* Line  8 is the person we are checking on */
                when line = 8
                     then userid=data
                /* Line 10, Column 40 is the month field */
                when (line = 10) & (column = 40)
                     then month=right(data,2,"0")
                /* Line 10, Column 45 is the day field */
                when (line = 10) & (column = 45)
                     then day=right(data,2,"0")
                /* Line 10, Column 50 is the year field */
                when (line = 10) & (column = 50)
                     then year=right(data,2,"0")
                /* Line 12 is the number of consecutive days field */
                when line = 12
                     then consdays=right(data,2,"0")
                otherwise nop
            end
 
        end
 
        /* If the customer pressed a PF key, process it */
        if tag = "PFK" then do
 
            select
                /* If the customer pressed PF1, PF13, PF9, or    */
                /* PF21 then help is requested on the panel.     */
                when (line = 1)  | (line = 13) |,
                     (line = 9)  | (line = 21) then status=3
                /* If the customer pressed PF3, PF15, PF12, or PF24 */
                /* then the customer wishes to exit.                */
                when (line = 3)  | (line = 15) |,
                     (line = 12) | (line = 24) then do
                    "DESBUF"
                    status=2
                    leave
                end
                /* Otherwise, the PF key is not defined to us. */
                otherwise
                    message="PFKey is not defined."
                    call display_message
            end
 
        end
 
        /* If the customer pressed the ENTER key, then set the */
        /* status flag to "check/process input panel".         */
        if tag = "ETK" then status=0
 
    end
 
    /* The customer requested assistance for the panel */
    /* or XEDIT options.                               */
    if status = 3 then do
        address command "HELP CALVIEW"
        /* Now, resume panel input/processing */
        status=1
    end
 
    /* Was panel checking/processing requested? */
    if status = 0 then do
 
        /* Check to see if we have any "error" conditions... */
        select
            /* Scenario 1:  The customer has not supplied a userid */
            when (userid = "") then do
                status=1
                message="A userid must be supplied to view a calendar."
                call display_message
                address xedit "CURSOR SCREEN 8 40"
            end
            /* Scenario 2:  The customer has supplied an invalid month */
            when (month ^= "") & (((month < 1) |,
                 (month > 12)) |,
                 (datatype(month,"NUMBER") ^= 1)) then do
                status=1
                message="The month must be a value between",
                        "1 and 12 inclusive."
                call display_message
                address xedit "CURSOR SCREEN 10 40"
            end
            /* Scenario 3:  The customer has supplied an invalid day */
            when (day ^= "") & (((day < 1) |,
                 (day > 31)) |,
                 (datatype(day,"NUMBER") ^= 1)) then do
                status=1
                message="The day must be a value between",
                        "1 and 31 inclusive."
                call display_message
                address xedit "CURSOR SCREEN 10 45"
            end
            /* Scenario 4:  The customer has supplied an invalid year */
            when ((year ^= "") &,
                 (datatype(year,"NUMBER") ^= 1)) then do
                status=1
                message="The year must be a numeric value."
                call display_message
                address xedit "CURSOR SCREEN 10 50"
            end
            /* Scenario 5:  The customer has supplied an invalid */
            /*              number of consecutive days           */
            when (consdays ^= "") & (((consdays < 1) |,
                 (consdays > 99)) |,
                 (datatype(consdays,"NUMBER") ^= 1)) then do
                status=1
                message="The number of consecutive days",
                        "must be a value between 1 and 99 inclusive."
                call display_message
                address xedit "CURSOR SCREEN 12 40"
            end
            otherwise nop
        end
 
    end
 
    /* If status is still zero then we have passed the panel checks. */
    /* Now let us construct and perform the command for the customer.*/
    if status = 0 then do
      /* Remove the PF key display to indicate the PF key functions */
      /* are no longer available.                                   */
      address xedit "CURSOR SCREEN 22 80"
      address xedit "SET RESERVED -1 N"
      address xedit "SET RESERVED  8 N"
      address xedit "SET RESERVED 10 N"
      address xedit "SET RESERVED 12 N"
      address xedit "SET RESERVED  4 OFF"
      address xedit "SET RESERVED  5 OFF"
      address xedit "SET MSGLINE ON 4 4"
      address xedit "REFRESH"
      obt_cal_status=ObtainCalendar()
      select
        when (obt_cal_status = "NOPROBLEM") then do
          queue userid
          queue trunc(month)
          queue trunc(day)
          queue trunc(year)
          queue trunc(consdays)
          address cms "XEDIT $$CAL$$" userid "A (PROFILE CALVIEWV)"
          status=2
        end
        when (obt_cal_status = "SERVERNOTAVAILABLE") then do
          message="The calendar server is currently not available.",
                  "Please report this message to the HelpDesk at",
                  "502/852-7997."
          call display_message
          address xedit "SET RESERVED -1 N %@PF3/PF12%! Quit"
          do index=1 to 24
            address xedit "SET PF"index "BEFORE EMSG Press PF3/PF12 to",
                          "quit. The calendar server is not available.",
                          "For further      assistance, please contact",
                          "the HelpDesk at 502/852-7997."
          end
          address xedit "SET PF3  BEFORE QQUIT"
          address xedit "SET PF12 BEFORE QQUIT"
          address xedit "SET PF15 BEFORE QQUIT"
          address xedit "SET PF24 BEFORE QQUIT"
        end
        when (obt_cal_status = "PROBLEM") then do
          message="Unable to read in the calendar on filemode A.  Press",
                  "PF3/PF12 to quit."
          call display_message
          address xedit "SET RESERVED -1 N %@PF3/PF12%! Quit"
          do index=1 to 24
            address xedit "SET PF"index "BEFORE EMSG Press PF3/PF12 to",
                          "quit.  Correct the problem and try again.",
                          "For further assistance, please contact the",
                          "HelpDesk at 502/852-7997."
          end
          address xedit "SET PF3  BEFORE QQUIT"
          address xedit "SET PF12 BEFORE QQUIT"
          address xedit "SET PF15 BEFORE QQUIT"
          address xedit "SET PF24 BEFORE QQUIT"
        end
        when (obt_cal_status = "NOTACCEPTING") then do
          message="The calendar server is currently not accepting",
                  "requests.  Please report this message to the",
                  "HelpDesk at 502/852-7997."
          call display_message
          address xedit "SET RESERVED -1 N %@PF3/PF12%! Quit"
          do index=1 to 24
            address xedit "SET PF"index "BEFORE EMSG Press PF3/PF12 to",
                          "quit. The calendar server is not accepting",
                          "requests. For     further assistance, please",
                          "contact the HelpDesk at 502/852-7997."
          end
          address xedit "SET PF3  BEFORE QQUIT"
          address xedit "SET PF12 BEFORE QQUIT"
          address xedit "SET PF15 BEFORE QQUIT"
          address xedit "SET PF24 BEFORE QQUIT"
        end
        when (obt_cal_status = "NOTRESPONDED") then do
          message="The calendar server has not responded after several",
                  "attempts.  Please report this message to the",
                  "HelpDesk at 502/852-7997."
          call display_message
          address xedit "SET RESERVED -1 N %@PF3/PF12%! Quit"
          do index=1 to 24
            address xedit "SET PF"index "BEFORE EMSG Press PF3/PF12 to",
                          "quit. The calendar server did not respond",
                          "within the         allotted time. For",
                          "further assistance, please contact the",
                          "HelpDesk at          502/852-7997."
          end
          address xedit "SET PF3  BEFORE QQUIT"
          address xedit "SET PF12 BEFORE QQUIT"
          address xedit "SET PF15 BEFORE QQUIT"
          address xedit "SET PF24 BEFORE QQUIT"
        end
        otherwise nop
      end
    end
end
 
if (status = 2) then address xedit "QQUIT"
exit
 
ObtainCalendar:
 
ObtCalStatus="PROBLEM"
 
address command
 
bypass_transmission=0
 
address xedit "refresh"
 
/* Check to make sure that the calendar service machine is up...*/
"PIPE CP QUERY USER STUDENTS | HOLE"
if RC ^= 0 then do
  bypass_transmission=1
  ObtCalStatus="SERVERNOTAVAILABLE"
end
 
if bypass_transmission <> 1 then do
 
  record_to_submit=userid,
                   month||"/"||day||"/"||year,
                   consdays
 
  message="Sending request to calendar server... Please wait..."
  call display_message
 
  number_of_attempts=1
 
  "PIPE CP SPOOL RDR CLASS Z | HOLE"
 
  do loop=1
 
    address xedit "REFRESH"
    "PIPE CP SMSG STUDENTS" record_to_submit "| HOLE"
    if RC ^= 0 then do
      ObtCalStatus="NOTACCEPTING"
      leave loop
    end
    "PIPE CP SET IMSG OFF | HOLE"
    "WAKEUP +00:00:15 ( QUIET RDR"
 
    select
      when RC=2 then do
        "PIPE CP SET IMSG ON | HOLE"
        number_of_attempts=number_of_attempts+1
        if number_of_attempts <> 7 then do
          message="The server is responding slowly at this time. Your",
                  "request is being re-submitted (attempt",
                  number_of_attempts" of 6). Please wait..."
          call display_message
        end
        else do
          ObtCalStatus="NOTRESPONDED"
          leave loop
        end
      end
      when RC=4 then do
        "PIPE CP SET IMSG ON | HOLE"
        'PIPE CP QUERY RDR * CLASS Z AVAIL |',
        '     DROP FIRST                   |',
        '     STEM classz_files.           |',
        '     HOLE'
        if (classz_files.0 ^= 1) then try_again=1
        else do
          parse upper var classz_files.1 origin_id cal_spool_id .
          if (origin_id ^= "STUDENTS") then try_again=1
          else do
            "PIPE CMS RECEIVE" cal_spool_id "= = A ( NOLOG REPLACE ) |",
            "     HOLE"
            if (RC ^= 0) then do
              "PIPE CP PURGE * RDR" cal_spool_id "| HOLE"
              try_again=0
            end
            else do
              ObtCalStatus="NOPROBLEM"
              try_again=0
            end
          end
        end
        if try_again ^= 1 then leave loop
      end
      otherwise nop
    end
  end
end
 
"PIPE CP SPOOL RDR CLASS * | HOLE"
 
return ObtCalStatus
 
/* Construct the input panel for the customer... */
BuildDisplay:
address xedit
"SET RESERVED  1 N %!CALVIEW                      %@VIEW",
                   "CALENDARS%!                      LEVEL"level
"SET RESERVED  4 N %@To view someone's calendar, supply their userid. ",
                   "You may also change the date%!"
"SET RESERVED  5 N %@and the number of consecutive days to view.",
                   "Then press Enter.%!"
"SET RESERVED  8 N %!                     ",
                   "UserID         %$"left(userid,8,"_")"%!"
"SET RESERVED 10 N %!                     ",
   "Date          ",
   "%$"left(month,2,"_")"%!/%$"left(day,2,"_")"%!/%$"left(year,2,"_")"%!"
"SET RESERVED 12 N %!                     ",
                   "Number of Days",
                   "%$"left(consdays,2,"_")"%!"
"SET RESERVED -1 N %@PF1/PF9%! Help     %@PF3/PF12%! Quit"
address command
return
 
display_message:
 
if message ^= "" then do
  /* Now format the message for proper display */
  nextword=""
  msglines.0=1
  msgindex=msglines.0
  do index=1 to 2
    msglines.index=""
  end
  do index=1 to words(message)
    msgindex=msglines.0
    nextword=word(message,index)
    /* If we can fit the next word into the line, then do so */
    if (length(msglines.msgindex)+length(nextword)) <= 78 then
      if length(msglines.msgindex) = 0 then
        msglines.msgindex=nextword
      else
        msglines.msgindex=msglines.msgindex||" "||nextword
    /* Otherwise, pad the rest of the current line, */
    /* and start a new line.                        */
    else do
      msglines.msgindex=left(msglines.msgindex,79)
      msglines.0=msglines.0+1
      msgindex=msglines.0
      msglines.msgindex=nextword
    end
  end
  msgtodisplay=""
  do index=1 to msglines.0
    msgtodisplay=msgtodisplay||msglines.index
  end
  address xedit "EMSG" msgtodisplay
  message=""
end
 
return
