/*********************************************************************/                                                           
/*                                                                   */                                                           
/*                    University of Louisville                       */                                                           
/*                Non-OV Calendar View Service Machine Code          */                                                           
/*                                                                   */                                                           
/* Exit Code Definitions:                                            */                                                           
/*   Not Applicable                                                  */                                                           
/*                                                                   */                                                           
/* Required Files:                                                   */                                                           
/*   STUDENTS AUTHORIZ A   Contains userids who have authorized      */                                                           
/*                         viewing of their calendars.               */                                                           
/*   INFO     CODES    A   Contains return codes from OFFICE         */                                                           
/*                         APPOINTM that are to be returned as       */                                                           
/*                         informational messages to the customer.   */                                                           
/*   STUDENTS ADMIN    A   Contains userids authorized to            */                                                           
/*                         administrate STUDENTS.                    */                                                           
/*                                                                   */                                                           
/* Mod Date  Init     Description                                    */                                                           
/* --------  -------  ---------------------------------------------- */                                                           
/* 01261995  BAJ/PDL  Initial Development                            */                                                           
/* 02031995  PDL      Changed results transmission method from the   */                                                           
/*                    SENDFILE command to a combination of           */                                                           
/*                    PUNCH/CHANGE/TRANSFER commands.  This is so    */                                                           
/*                    the WAKEUP on the client side can distinguish  */                                                           
/*                    incoming calendar results via class Z reader   */                                                           
/*                    files.                                         */                                                           
/* 02031995  PDL      Added calendar viewing authorization file      */                                                           
/*                    code to check and make sure that the calendar  */                                                           
/*                    requested is authorized for viewing.           */                                                           
/* 02031995  PDL      Added code to discard the "$$CAL$$ userid"     */                                                           
/*                    generated by obtaining authorizations from the */                                                           
/*                    OV calendar service machine.  The presence of  */                                                           
/*                    the file was suspending processing because     */                                                           
/*                    OV would ask for permission to overwrite the   */                                                           
/*                    file when getting the actual calendar.         */                                                           
/* 02031995  PDL      Added code to log all incoming and outgoing    */                                                           
/*                    transactions.                                  */                                                           
/* 02061995  PDL      Re-arranged logic in the AuthorizeToView       */                                                           
/*                    function.                                      */                                                           
/* 02071995  PDL      Added code to handle various return codes      */                                                           
/*                    from OFFICE APPOINTM GETCAL command.           */                                                           
/* 02071995  PDL      Added code to provide administration services  */                                                           
/*                    for authorized userids.  Current services are  */                                                           
/*                    CP, CMS, STOP, STOP LOGOFF.                    */                                                           
/* 02071995  PDL      Fixed code to obtain calendar authorizations   */                                                           
/*                    in AuthorizeToView.  Code did not properly     */                                                           
/*                    account for condition when requester had no    */                                                           
/*                    authorization to view calendar.  OV function   */                                                           
/*                    used does not return an authorization file in  */                                                           
/*                    this case; rather, it returns rc=76.           */                                                           
/* 02131995  BAJ      Added check for rc 12: not known OV id.        */                                                           
/*                    Reworded some of the 'not authorized' messages.*/                                                           
/* 02141995  BAJ      Check for calendar authorization before we     */                                                           
/*                    check if they are in the STUDETNS AUTHORIZ file*/                                                           
/*                    so 'not OV' message is returned rather than    */                                                           
/*                    'not authorized userid' msg if not OV userid.  */                                                           
/*                    Also moved the erase on old $$CAL$$ Userid     */                                                           
/*                    to happen all the time before we need it.      */                                                           
/* 02141995  BAJ      Add code back in to check authorization file   */                                                           
/*                    for situation when OV user is using CALVIEW    */                                                           
/*                    but they have been restricted from viewing     */                                                           
/*                    the calendar they are requesting.              */                                                           
/* 04251995  BAJ      Added code to reject requests before 4/24/95   */                                                           
/* 05261995  BAJ      Removed STUDENTS AUTHORIZ for full production. */                                                           
/*********************************************************************/                                                           
                                                                                                                                  
'CP SPOOL CONSOLE BAJONE02 START NAME STUDENTS SERVRLOG'                                                                          
                                                                                                                                  
trace off                                                                                                                         
                                                                                                                                  
Quit_flag= 'NO'                                                                                                                   
                                                                                                                                  
                                                                                                                                  
/* Check to make sure INFO CODES file exists, if not abort. */                                                                    
'PIPE CMS STATE INFO CODES A | HOLE'                                                                                              
if (RC ^= 0) then do                                                                                                              
  say 'The STUDENTS server could not locate the INFO CODES file.'                                                                 
  say 'STUDENTS server processing will stop.'                                                                                     
  say ''                                                                                                                          
  Quit_flag='YES'                                                                                                                 
end                                                                                                                               
                                                                                                                                  
/* Check to make sure STUDENTS ADMIN file exists, if not abort. */                                                                
'PIPE CMS STATE STUDENTS ADMIN A | HOLE'                                                                                          
if (RC ^= 0) then do                                                                                                              
  say 'The STUDENTS server could not locate the STUDENTS ADMIN file.'                                                             
  say 'STUDENTS server processing will stop.'                                                                                     
  say ''                                                                                                                          
  Quit_flag='YES'                                                                                                                 
end                                                                                                                               
                                                                                                                                  
say 'STUDENTS server processing online.'                                                                                          
                                                                                                                                  
do while (Quit_flag^="YES")                                                                                                       
                                                                                                                                  
  say 'Waiting for CP SMSG transaction...'                                                                                        
  /* Wakeup for console interrupt or CP smsg */                                                                                   
  'WAKEUP ( SMSG CONS'                                                                                                            
                                                                                                                                  
  select  /* major select to evaluate WAKEUP responses */                                                                         
                                                                                                                                  
    when (rc=1) then do  /* SMSG received */                                                                                      
                                                                                                                                  
      /* Get SMSG data and process */                                                                                             
      parse upper pull . Calrequester smsgdata                                                                                    
                                                                                                                                  
      "PIPE < STUDENTS ADMIN A |",                                                                                                
      "     STEM Serv_Admin.   |",                                                                                                
      "     HOLE"                                                                                                                 
                                                                                                                                  
      parse upper var smsgdata admindirect command commanddata                                                                    
      if (admindirect = "ADMIN") then do                                                                                          
        not_valid_admin="TRUE"                                                                                                    
        say 'Received ADMIN command from' Calrequester'.'                                                                         
        say 'ADMIN command was:' smsgdata                                                                                         
        do admin_index = 1 to Serv_Admin.0                                                                                        
          if (Calrequester = Serv_Admin.admin_index) then do                                                                      
            not_valid_admin="FALSE"                                                                                               
            say 'Attempting to parse/execute ADMIN command.'                                                                      
            select /* validate Admin command values */                                                                            
              when (command = "STOP") then do                                                                                     
                quit_flag="YES"                                                                                                   
                "TELL" Calrequester "Server processing stopped."                                                                  
                if (commanddata = "LOGOFF") then logoffserver="TRUE"                                                              
              end                                                                                                                 
              when (command = "CMS") then do                                                                                      
                "PIPE CMS" commanddata "| STEM cmd_msgs. | HOLE"                                                                  
                cmdretcode=rc                                                                                                     
                do cmd_index=1 to cmd_msgs.0                                                                                      
                  "TELL" Calrequester cmd_msgs.cmd_index                                                                          
                  say "ADMIN Command result:" cmd_msgs.cmd_index                                                                  
                end                                                                                                               
                "TELL" Calrequester,                                                                                              
                  "Command return code="cmdretcode                                                                                
                say "ADMIN Command result: rc="cmdretcode                                                                         
              end                                                                                                                 
              when (command = "CP") then do                                                                                       
                "PIPE CP" commanddata "| STEM cmd_msgs. | HOLE"                                                                   
                cmdretcode=rc                                                                                                     
                do cmd_index=1 to cmd_msgs.0                                                                                      
                  "TELL" Calrequester cmd_msgs.cmd_index                                                                          
                  say "ADMIN Command result:" cmd_msgs.cmd_index                                                                  
                end                                                                                                               
                "TELL" Calrequester,                                                                                              
                  "Command return code="cmdretcode                                                                                
                say "ADMIN Command result: rc="cmdretcode                                                                         
              end                                                                                                                 
              otherwise nop                                                                                                       
                "TELL" Calrequester "UNKNOWN ADMIN COMMAND."                                                                      
            end /* end on select for validation of command values */                                                              
          end /* end on do when requestor is in admin file */                                                                     
        end  /* end on do loop checking serv_admin entries */                                                                     
        if (not_valid_admin = "TRUE") then do                                                                                     
          "TELL" Calrequester "You are not authorized to administer",                                                             
                              "this service machine."                                                                             
          say '***** WARNING:  Invalid attempt to administrate by',                                                               
              Calrequester '*****'                                                                                                
        end                                                                                                                       
      end   /* end for do if ADMIN request made */                                                                                
      else do   /* Not ADMIN request */                                                                                           
                                                                                                                                  
        parse upper var smsgdata Caluser Caldate Caldays .                                                                        
                                                                                                                                  
        say 'Received CP SMSG transaction from' Calrequester'.'                                                                   
        say Calrequester' is requesting the calendar for 'Caluser,                                                                
            '('Caldate', 'Caldays').'                                                                                             
                                                                                                                                  
       /* Test request date and reject all before                                                                                 
           implementation date: 4/24/95 */                                                                                        
                                                                                                                                  
        CalMo=substr(Caldate,1,2)                                                                                                 
        Calyear=right(Caldate,2)                                                                                                  
        Calday=substr(Caldate,4,2)                                                                                                
        if Calyear||Calmo||Calday <  '950424' then                                                                                
            Authorized_result = "TOO_EARLY"                                                                                       
        else                                                                                                                      
           Authorized_result=AuthorizedToView(Caluser,Calrequester)                                                               
                                                                                                                                  
        select   /* Evaluating Authorized_result */                                                                               
          when (Authorized_result = "TOO_EARLY") then do                                                                          
             /* Reject view requests before April 24, 1995 */                                                                     
            'PIPE LITERAL You may not view calendars prior to',                                                                   
                         'April 24, 1995, using CALVIEW. |',                                                                      
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          when (Authorized_result = "NO_VIEW_ALLOWED") then do                                                                    
             /* Calendar owner has restricted this OV customer */                                                                 
            'PIPE LITERAL' Caluser 'has not authorized you to',                                                                   
                         'view the calendar. |',                                                                                  
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          when (Authorized_result = "NO") then do                                                                                 
             /* Calendar being sought has STUDENTS userid restricted */                                                           
            'PIPE LITERAL' Caluser 'has restricted your view of',                                                                 
                         'the calendar through this service. |',                                                                  
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          when (Authorized_result = "NOT_SERVER_AUTHORIZED") then do                                                              
            /* This test goes away when we open for full production */                                                            
            'PIPE LITERAL This service is currently only available',                                                              
                         'for viewing limited calendars. |',                                                                      
            ' APPEND LITERAL' Caluser 'is not one of them.|',                                                                     
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          when (Authorized_result = "NOTOV") then do                                                                              
            'PIPE LITERAL' Caluser 'is not a known OfficeVision',                                                                 
                         'userid. |',                                                                                             
            'APPEND LITERAL No calendar is available.|',                                                                          
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          when (Authorized_result = "PROBLEM") then do                                                                            
            'PIPE LITERAL Calendar cannot be retrieved at this time. |',                                                          
            '     > $$CAL$$' Caluser 'A |',                                                                                       
            '     HOLE'                                                                                                           
          end                                                                                                                     
          otherwise /* Handles YES authorization */                                                                               
            'PIPE < INFO CODES A     |',                                                                                          
            '     STEM Info_Codes.   |',                                                                                          
            '     HOLE'                                                                                                           
                                                                                                                                  
        /* Make sure there is not an old file left on A already. */                                                               
        'PIPE CMS STATE $$CAL$$' Caluser 'A | HOLE'                                                                               
        if (rc = 0) then 'ERASE $$CAL$$' Caluser 'A'                                                                              
                                                                                                                                  
            'PIPE CMS OFFICE APPOINTM GETCAL',                                                                                    
                      Caluser Caldate Caldays 'FILE |',                                                                           
            '     STEM Getcal_Messages.             |',                                                                           
            '     HOLE'                                                                                                           
                                                                                                                                  
            if (rc ^= 0) then do                                                                                                  
              bypass_critical='FALSE'                                                                                             
                                                                                                                                  
              do rc_index= 1 to Info_Codes.0                                                                                      
                if (rc = Info_Codes.rc_index) then do                                                                             
                  'PIPE STEM Getcal_Messages. |',                                                                                 
                  '     > $$CAL$$' Caluser 'A |',                                                                                 
                  '     HOLE'                                                                                                     
                  bypass_critical='TRUE'                                                                                          
                end                                                                                                               
              end                                                                                                                 
                                                                                                                                  
              if (bypass_critical = 'FALSE') then do                                                                              
                'PIPE STEM Getcal_Messages.        |',                                                                            
                '     APPEND LITERAL               |',                                                                            
                '     APPEND LITERAL PROCESSING FAILURE:  Please report',                                                         
                             'the message above to the HelpDesk at |',                                                            
                '     APPEND LITERAL 502/852-7997. |',                                                                            
                '     > $$CAL$$' Caluser 'A        |',                                                                            
                '     HOLE'                                                                                                       
              end                                                                                                                 
            end                                                                                                                   
        end      /* end for Select to Evaluate Authorized_result */                                                               
                                                                                                                                  
        'PUNCH $$CAL$$' Caluser 'A'                                                                                               
        'CHANGE * PUN ALL CLASS Z'                                                                                                
        'TRANSFER PUN ALL TO' Calrequester 'RDR'                                                                                  
        'ERASE $$CAL$$' Caluser 'A'                                                                                               
        say 'Transmitted calendar query results to' Calrequester'.'                                                               
      end  /* End for Not Admin Request processing */                                                                             
    end   /* end for WAKEUP rc = 1 processing */                                                                                  
                                                                                                                                  
    when (rc=6) then Quit_flag="YES"   /* Console interrupt */                                                                    
                                                                                                                                  
    otherwise nop                                                                                                                 
  end  /* end for first select to evaluate WAKEUP responses */                                                                    
end   /* end major Do While on Quit_flag test */                                                                                  
                                                                                                                                  
say 'STUDENTS service machine processing stopped.'                                                                                
                                                                                                                                  
if (logoffserver = "TRUE") then do                                                                                                
  "TELL" Calrequester "STUDENTS service machine logged off."                                                                      
  queue "LOGOFF"                                                                                                                  
end                                                                                                                               
                                                                                                                                  
exit                                                                                                                              
                                                                                                                                  
AuthorizedToView: procedure                                                                                                       
                                                                                                                                  
  Arg Caluser,Calrequester                                                                                                        
                                                                                                                                  
    /* Make sure there is not an old file left on A already. */                                                                   
    'PIPE CMS STATE $$CAL$$' Caluser 'A | HOLE'                                                                                   
    if (rc = 0) then 'ERASE $$CAL$$' Caluser 'A'                                                                                  
                                                                                                                                  
    'OFFICE APPOINTM AU' Caluser 'FILE'                                                                                           
                                                                                                                                  
    select                                                                                                                        
      when (rc = 12) then                                                                                                         
        authorize_status="NOTOV"                                                                                                  
      when (rc = 76) then                                                                                                         
        authorize_status="NO"                                                                                                     
      when (rc = 0)  then do   /* Authorization file returned */                                                                  
        'PIPE < $$CAL$$' Caluser' A  |',                                                                                          
        '   LOCATE /'Calrequester'/  |',                                                                                          
        '   VAR Auth_results         |',                                                                                          
        '   HOLE'                                                                                                                 
        if (Auth_results = "AUTH_RESULTS") then   /* no restrictions */                                                           
            authorize_status = "YES"                                                                                              
        else do  /* Found requestor userid in the authorization list */                                                           
           Time_auth=substr(Auth_results,41,1)                                                                                    
           View_auth=substr(Auth_results,46,1)                                                                                    
           if ((Time_auth = "N") & (View_auth = "N")) then                                                                        
              authorize_status = "NO_VIEW_ALLOWED"                                                                                
        end                                                                                                                       
      end  /* end on do when authorization file returned  */                                                                      
      otherwise do  /*  (rc ^= 0) */                                                                                              
        authorize_status="PROBLEM"                                                                                                
        say 'STUDENTS could not obtain calendar authorizations for',                                                              
            Caluser'.'                                                                                                            
      end                                                                                                                         
   'ERASE $$CAL$$' Caluser 'A'                                                                                                    
                                                                                                                                  
    end   /* end on select */                                                                                                     
                                                                                                                                  
return authorize_status                                                                                                           