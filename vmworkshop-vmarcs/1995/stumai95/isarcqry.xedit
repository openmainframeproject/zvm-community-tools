/*********************************************************************/                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* ISARCQRY XEDIT                                                    */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Usage:  ISARCQRY is an XEDIT-based interactive input panel        */                                                                                                                                                                                        
/*         designed to be invoked from the ISARC EXEC.               */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* ISARCQRY is an interactive input panel which asks the customer    */                                                                                                                                                                                        
/* for required information (SSN), and returns this information to   */                                                                                                                                                                                        
/* the ISARC EXEC.    This information will be used to search        */                                                                                                                                                                                        
/* an account creation results file(s) containing any error          */                                                                                                                                                                                        
/* information for the SSN supplied.                                 */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Exit Code Definitions:                                            */                                                                                                                                                                                        
/*   Exit Codes are not used.  Results returned via the program      */                                                                                                                                                                                        
/*   stack:                                                          */                                                                                                                                                                                        
/*     o Literal text "EXECUTE" returned if STUDREQ is to perform    */                                                                                                                                                                                        
/*       the account query.  Other information passed includes the   */                                                                                                                                                                                        
/*       SSN.                                                        */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/*     o Literal text "DO NOT EXECUTE" returned if STUDREQ is not    */                                                                                                                                                                                        
/*       to perform the account query.                               */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Required Files:                                                   */                                                                                                                                                                                        
/*   ISARC    EXEC     (Calling program)                             */                                                                                                                                                                                        
/*   ISARCQRY ISARCHLP (Help for the account query input panel)      */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Mod Date  Init  Description                                       */                                                                                                                                                                                        
/* --------  ----  ------------------------------------------------- */                                                                                                                                                                                        
/* 07181994  PDL   Initial Development                               */                                                                                                                                                                                        
/* 07291994  PDL   Program/modules names changed.                    */                                                                                                                                                                                        
/* 07291994  PDL   Changed to new help display method.               */                                                                                                                                                                                        
/* 07291994  PDL   Changed PF1 key wording to "Help".                */                                                                                                                                                                                        
/* 07291994  PDL   Changed userid display to mod/level display.      */                                                                                                                                                                                        
/* 07291994  PDL   Changed description text to read "To check the    */                                                                                                                                                                                        
/*                 status of an account, enter your Social Security  */                                                                                                                                                                                        
/*                 Number and press PF6.".                           */                                                                                                                                                                                        
/* 07291994  PDL   Expanded/Changed screen title to occupy 2 lines.  */                                                                                                                                                                                        
/* 08011994  PDL   Modified code to request confirmation of data     */                                                                                                                                                                                        
/*                 prior to actual query submission.                 */                                                                                                                                                                                        
/* 08011994  PDL   Various message text changes.                     */                                                                                                                                                                                        
/* 08041994  PDL   Added code to detect presence of ISARCHLP module  */                                                                                                                                                                                        
/*                 prior to displaying help.                         */                                                                                                                                                                                        
/* 09071994  PDL   Added code to override display of a possible error*/                                                                                                                                                                                        
/*                 message when help (PF1, etc.) is requested.       */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/*********************************************************************/                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
trace off                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
level="110"                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
/* Make sure we have no extraneous information on the stack. */                                                                                                                                                                                                
"DESBUF"                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                               
/* Main part of ISARCQRY.  Call routines from here and exit out. */                                                                                                                                                                                            
address command                                                                                                                                                                                                                                                
call prepare_full_screen_environment                                                                                                                                                                                                                           
call initialize_variables                                                                                                                                                                                                                                      
call process_input                                                                                                                                                                                                                                             
address xedit "QQUIT"                                                                                                                                                                                                                                          
exit                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                               
/****************************************************************/                                                                                                                                                                                             
/* Input panel processing routine.  This routine issues a XEDIT */                                                                                                                                                                                             
/* screen READ and parses that information.                     */                                                                                                                                                                                             
/****************************************************************/                                                                                                                                                                                             
process_input:                                                                                                                                                                                                                                                 
/* We do not actually need to specify that we loop once, but    */                                                                                                                                                                                             
/* this will give the loop a name in case we must LEAVE it.     */                                                                                                                                                                                             
do loop = 1                                                                                                                                                                                                                                                    
  /* First, let us display the screen */                                                                                                                                                                                                                       
  call display_screen                                                                                                                                                                                                                                          
  address xedit "READ NOCHANGE NUMBER TAG"                                                                                                                                                                                                                     
  /* Now let us process the information returned from the XEDIT READ */                                                                                                                                                                                        
  pfkeypressed=""                                                                                                                                                                                                                                              
  enterkeypressed=""                                                                                                                                                                                                                                           
  do queued()                                                                                                                                                                                                                                                  
    parse pull key line col text                                                                                                                                                                                                                               
    /* Prevent escapes from being acted upon */                                                                                                                                                                                                                
    text=translate(text," ","%")                                                                                                                                                                                                                               
    /* In this part, we will preserve any PF keypresses for later, */                                                                                                                                                                                          
    /* so that data for the fields can be processed beforehand.    */                                                                                                                                                                                          
    select                                                                                                                                                                                                                                                     
      when key = "PFK" then pfkeypressed=line                                                                                                                                                                                                                  
      when key = "RES" then do                                                                                                                                                                                                                                 
        call process_fields                                                                                                                                                                                                                                    
        submit_confirmation_flag="NONE"                                                                                                                                                                                                                        
      end                                                                                                                                                                                                                                                      
      when key = "ETK" then enterkeypressed="YES"                                                                                                                                                                                                              
      otherwise nop                                                                                                                                                                                                                                            
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Check to see if there are any errors before we get to the    */                                                                                                                                                                                           
  /* PF key processing and ENTER key processing.                  */                                                                                                                                                                                           
  call process_errors                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Process the Enter key, if pressed. */                                                                                                                                                                                                                     
  if (enterkeypressed ^= "") &,                                                                                                                                                                                                                                
     (error_flag ^= "THERE ARE ERRORS") then do                                                                                                                                                                                                                
    enterkeypressed=""                                                                                                                                                                                                                                         
    select                                                                                                                                                                                                                                                     
      when submit_confirmation_flag = "NONE" then do                                                                                                                                                                                                           
        submit_confirmation_flag="READY"                                                                                                                                                                                                                       
        message="If the above information is correct, press",                                                                                                                                                                                                  
                "the PF6 key to submit your request."                                                                                                                                                                                                          
        call display_message                                                                                                                                                                                                                                   
      end                                                                                                                                                                                                                                                      
      when submit_confirmation_flag = "READY" then do                                                                                                                                                                                                          
        message="You must press the PF6 key to submit your request."                                                                                                                                                                                           
        call display_message                                                                                                                                                                                                                                   
      end                                                                                                                                                                                                                                                      
      otherwise nop                                                                                                                                                                                                                                            
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Now let us process PF keypresses... */                                                                                                                                                                                                                    
  if pfkeypressed ^= "" then do                                                                                                                                                                                                                                
    line=pfkeypressed                                                                                                                                                                                                                                          
    pfkeypressed=""                                                                                                                                                                                                                                            
    select                                                                                                                                                                                                                                                     
      when (line = 1)  | (line = 9)  |,                                                                                                                                                                                                                        
           (line = 13) | (line = 21) then do                                                                                                                                                                                                                   
        "PIPE CMS STATE ISARCQRY ISARCHLP * | HOLE"                                                                                                                                                                                                            
        retcode=RC                                                                                                                                                                                                                                             
        "PIPE CMS STATE ISARCHLP XEDIT * | HOLE"                                                                                                                                                                                                               
        if retcode=0 then retcode=RC                                                                                                                                                                                                                           
        if retcode ^= 0 then                                                                                                                                                                                                                                   
          message="ERROR QRY001: Help for this screen (ISARCQRY) is",                                                                                                                                                                                          
                  "currently not available. Please report this message",                                                                                                                                                                                       
                  "to the HelpDesk at 502/852-7997."                                                                                                                                                                                                           
        else do                                                                                                                                                                                                                                                
          address cms "XEDIT ISARCQRY ISARCHLP * (PROFILE",                                                                                                                                                                                                    
                      "ISARCHLP NOLOCK)"                                                                                                                                                                                                                       
          /* 09071994 ADDITION:  Added code to override display of a */                                                                                                                                                                                        
          /*                     possible error message.             */                                                                                                                                                                                        
          message=""                                                                                                                                                                                                                                           
        end                                                                                                                                                                                                                                                    
      end                                                                                                                                                                                                                                                      
      when (line = 3)  | (line = 12) |,                                                                                                                                                                                                                        
           (line = 15) | (line = 24) then do                                                                                                                                                                                                                   
        DESBUF                                                                                                                                                                                                                                                 
        queue "DO NOT EXECUTE"                                                                                                                                                                                                                                 
        leave loop                                                                                                                                                                                                                                             
      end                                                                                                                                                                                                                                                      
      when (line = 6) | (line = 18)  then do                                                                                                                                                                                                                   
        if (error_flag ^= "THERE ARE ERRORS") then do                                                                                                                                                                                                          
          select                                                                                                                                                                                                                                               
            when submit_confirmation_flag = "NONE" then do                                                                                                                                                                                                     
              submit_confirmation_flag="READY"                                                                                                                                                                                                                 
              message="If the above information is correct, press",                                                                                                                                                                                            
                      "the PF6 key again to submit your request."                                                                                                                                                                                              
              call display_message                                                                                                                                                                                                                             
            end                                                                                                                                                                                                                                                
            when submit_confirmation_flag = "READY" then do                                                                                                                                                                                                    
              call perform_execution                                                                                                                                                                                                                           
              if exec_flag = "Done" then leave loop                                                                                                                                                                                                            
            end                                                                                                                                                                                                                                                
            otherwise nop                                                                                                                                                                                                                                      
          end                                                                                                                                                                                                                                                  
        end                                                                                                                                                                                                                                                    
      end                                                                                                                                                                                                                                                      
      otherwise message = "You have pressed an invalid PF key.  Valid",                                                                                                                                                                                        
                          "PF keys are shown below."                                                                                                                                                                                                           
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
end                                                                                                                                                                                                                                                            
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/**********************************************************/                                                                                                                                                                                                   
/* This routine will queue all return data items if there */                                                                                                                                                                                                   
/* are no input errors preventing request submission.     */                                                                                                                                                                                                   
/**********************************************************/                                                                                                                                                                                                   
perform_execution:                                                                                                                                                                                                                                             
exec_flag = "Not Done"                                                                                                                                                                                                                                         
if (error_flag == "THERE ARE ERRORS") then correct_before_exec = 1                                                                                                                                                                                             
else do                                                                                                                                                                                                                                                        
  queue "EXECUTE"                                                                                                                                                                                                                                              
  queue SSN                                                                                                                                                                                                                                                    
  exec_flag = "Done"                                                                                                                                                                                                                                           
end                                                                                                                                                                                                                                                            
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/******************************************************************/                                                                                                                                                                                           
/* This routine will perform error checks on all screen input     */                                                                                                                                                                                           
/* data and make sure that it is valid before request submission. */                                                                                                                                                                                           
/******************************************************************/                                                                                                                                                                                           
process_errors:                                                                                                                                                                                                                                                
error_flag = "THERE ARE NO ERRORS"                                                                                                                                                                                                                             
message=""                                                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                               
/* If the SSN is not of numeric type, then pinpoint exactly where */                                                                                                                                                                                           
/* the SSN is not numeric, and relay that to the customer by      */                                                                                                                                                                                           
/* positioning the cursor at the point of error.                  */                                                                                                                                                                                           
if datatype(SSN) ^= "NUM" then do                                                                                                                                                                                                                              
  do index=length(SSN) to 1 by -1                                                                                                                                                                                                                              
    if datatype(substr(SSN,index,1)) ^= "NUM" then location=index                                                                                                                                                                                              
  end                                                                                                                                                                                                                                                          
  err_col=36                                                                                                                                                                                                                                                   
  select                                                                                                                                                                                                                                                       
    when (location >= 1) & (location <= 3) then                                                                                                                                                                                                                
      err_col=err_col+location-1                                                                                                                                                                                                                               
    when (location >= 4) & (location <= 5) then                                                                                                                                                                                                                
      err_col=err_col+location-1+3                                                                                                                                                                                                                             
    when (location >= 6) & (location <= 9) then                                                                                                                                                                                                                
      err_col=err_col+location-1+6                                                                                                                                                                                                                             
    otherwise nop                                                                                                                                                                                                                                              
  end                                                                                                                                                                                                                                                          
  err_line = 12                                                                                                                                                                                                                                                
  message = "Your Social Security Number must be numeric."                                                                                                                                                                                                     
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
/* If the SSN is not exactly 9 digits in length, then         */                                                                                                                                                                                               
/* position the cursor after the last digit for the customer. */                                                                                                                                                                                               
if length(SSN) ^= 9 then do                                                                                                                                                                                                                                    
  SSNlength=length(SSN)                                                                                                                                                                                                                                        
  err_col=36                                                                                                                                                                                                                                                   
  select                                                                                                                                                                                                                                                       
    when (SSNlength >= 1) & (SSNlength <= 2) then                                                                                                                                                                                                              
      err_col=err_col+SSNlength                                                                                                                                                                                                                                
    when (SSNlength >= 3) & (SSNlength <= 4) then                                                                                                                                                                                                              
      err_col=err_col+SSNlength+3                                                                                                                                                                                                                              
    when (SSNlength >= 5) & (SSNlength <= 8) then                                                                                                                                                                                                              
      err_col=err_col+SSNlength+6                                                                                                                                                                                                                              
    otherwise nop                                                                                                                                                                                                                                              
  end                                                                                                                                                                                                                                                          
  err_line = 12                                                                                                                                                                                                                                                
  message = "Your Social Security Number must be 9 digits in length."                                                                                                                                                                                          
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
if (SSN = "") then do                                                                                                                                                                                                                                          
  err_line = 12                                                                                                                                                                                                                                                
  err_col = 36                                                                                                                                                                                                                                                 
  message = "Your Social Security Number is required."                                                                                                                                                                                                         
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
if message ^= "" then                                                                                                                                                                                                                                          
  error_flag = "THERE ARE ERRORS"                                                                                                                                                                                                                              
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/***************************************************************/                                                                                                                                                                                              
/* This routine takes the input data from the panel processing */                                                                                                                                                                                              
/* routine, and removes underscores and beginning and trailing */                                                                                                                                                                                              
/* blanks.  The data is then assigned to the appropriate       */                                                                                                                                                                                              
/* internal variable based on the source line number and source*/                                                                                                                                                                                              
/* column number.                                              */                                                                                                                                                                                              
/***************************************************************/                                                                                                                                                                                              
process_fields:                                                                                                                                                                                                                                                
error_flag = "NO ERRORS"                                                                                                                                                                                                                                       
text=strip(text,trailing,"_")                                                                                                                                                                                                                                  
text=strip(text)                                                                                                                                                                                                                                               
select                                                                                                                                                                                                                                                         
  when (line = 12) & (col = 36) then SSN_1=text                                                                                                                                                                                                                
  when (line = 12) & (col = 42) then SSN_2=text                                                                                                                                                                                                                
  when (line = 12) & (col = 47) then SSN_3=text                                                                                                                                                                                                                
  otherwise nop                                                                                                                                                                                                                                                
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
/* Form the SSN by concatenating all 3 SSN input fields. */                                                                                                                                                                                                    
SSN=SSN_1||SSN_2||SSN_3                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/**********************************************************************/                                                                                                                                                                                       
/* Set up the XEDIT screen for an input panel.  Un-RESERVE all lines, */                                                                                                                                                                                       
/* default all PF keys, enable/disable certain XEDIT features to make */                                                                                                                                                                                       
/* the screen conducive to an input panel.                            */                                                                                                                                                                                       
/**********************************************************************/                                                                                                                                                                                       
prepare_full_screen_environment:                                                                                                                                                                                                                               
address xedit "SET SCALE OFF"                                                                                                                                                                                                                                  
address xedit "SET PREFIX OFF"                                                                                                                                                                                                                                 
address xedit "SET TOFEOF OFF"                                                                                                                                                                                                                                 
address xedit "SET CTLCHAR % ESCAPE"                                                                                                                                                                                                                           
address xedit "SET CTLCHAR @ PROTECT   HIGH"                                                                                                                                                                                                                   
address xedit "SET CTLCHAR ! PROTECT   NOHIGH"                                                                                                                                                                                                                 
address xedit "SET CTLCHAR $ NOPROTECT HIGH"                                                                                                                                                                                                                   
address xedit "SET CTLCHAR  NOPROTECT INVISIBLE"                                                                                                                                                                                                              
address xedit "SET MSGLINE ON 21 2"                                                                                                                                                                                                                            
address xedit "SET CMDLINE OFF"                                                                                                                                                                                                                                
address xedit "SET MSGMODE ON"                                                                                                                                                                                                                                 
address xedit "SET LINEND OFF"                                                                                                                                                                                                                                 
address xedit "SET CASE MIXED"                                                                                                                                                                                                                                 
address xedit "VERIFY OFF 1 80"                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
do i = 1 to 24                                                                                                                                                                                                                                                 
  address xedit "SET PF"i "BEFORE EMSG ERROR QRY002: Internal failure.",                                                                                                                                                                                       
                "Exit immediately and report this message to the",                                                                                                                                                                                             
                "    HelpDesk at 502/852-7997."                                                                                                                                                                                                                
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
address xedit "SET PF3  BEFORE QQUIT"                                                                                                                                                                                                                          
address xedit "SET PF12 BEFORE QQUIT"                                                                                                                                                                                                                          
address xedit "SET PF15 BEFORE QQUIT"                                                                                                                                                                                                                          
address xedit "SET PF24 BEFORE QQUIT"                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
address xedit "EXTRACT /RESERVED */"                                                                                                                                                                                                                           
do index=1 to RESERVED.0                                                                                                                                                                                                                                       
  parse var RESERVED.index linenum otherstuff                                                                                                                                                                                                                  
  address xedit "SET RESERVED" linenum "OFF"                                                                                                                                                                                                                   
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/*************************************************************/                                                                                                                                                                                                
/* Initialize internal variables and default input fields to */                                                                                                                                                                                                
/* certain values if there is a need to do so.               */                                                                                                                                                                                                
/*************************************************************/                                                                                                                                                                                                
initialize_variables:                                                                                                                                                                                                                                          
userid = userid()                                                                                                                                                                                                                                              
SSN = ""                                                                                                                                                                                                                                                       
SSN_1 = ""                                                                                                                                                                                                                                                     
SSN_2 = ""                                                                                                                                                                                                                                                     
SSN_3 = ""                                                                                                                                                                                                                                                     
err_line = 12                                                                                                                                                                                                                                                  
err_col = 36                                                                                                                                                                                                                                                   
message = ""                                                                                                                                                                                                                                                   
error_flag="THERE ARE ERRORS"                                                                                                                                                                                                                                  
submit_confirmation_flag="NONE"                                                                                                                                                                                                                                
pfkeypressed=""                                                                                                                                                                                                                                                
enterkeypressed=""                                                                                                                                                                                                                                             
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
/*******************************************************************/                                                                                                                                                                                          
/* Display the actual input screen for the customer.  Accomplished */                                                                                                                                                                                          
/* by reserving the lines via XEDIT and PROTECTing/NOPROTECTing    */                                                                                                                                                                                          
/* the appropriate areas.  Also contains the message parsing code  */                                                                                                                                                                                          
/* which formats the message (up to 2 lines) for proper display.   */                                                                                                                                                                                          
/*******************************************************************/                                                                                                                                                                                          
display_screen:                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
address xedit                                                                                                                                                                                                                                                  
"SET RESERVED 1 NOH %!ISARCQRY ("level")          ",                                                                                                                                                                                                           
                     "%@University of Louisville%!              ",                                                                                                                                                                                             
                     left(date(),11)                                                                                                                                                                                                                           
"SET RESERVED 2  NOH %!                        ",                                                                                                                                                                                                              
                     "%@Individual Student Account%!"                                                                                                                                                                                                          
"SET RESERVED 3  NOH %!                        ",                                                                                                                                                                                                              
                     "      %@Request Status%!"                                                                                                                                                                                                                
"SET RESERVED 5  NOH %!To check the status of an account, enter your",                                                                                                                                                                                         
                      "Social Security Number and"                                                                                                                                                                                                             
"SET RESERVED 6  NOH %!press PF6."                                                                                                                                                                                                                             
"SET RESERVED 12 NOH %!                    ",                                                                                                                                                                                                                  
                     "       SSN:",                                                                                                                                                                                                                            
"%$"left(SSN_1,3,"_")"%!-%$"left(SSN_2,2,"_")"%!-%$"left(SSN_3,4,"_")"%!"                                                                                                                                                                                      
"SET RESERVED -1 NOH %@PF1/PF9%! Help                ",                                                                                                                                                                                                        
                    "%@PF3/PF12%! Quit",                                                                                                                                                                                                                       
                    "              %@PF6%! Submit Query"                                                                                                                                                                                                       
address command                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
display_message:                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                               
if message ^= "" then do                                                                                                                                                                                                                                       
  if (correct_before_exec == 1) then do                                                                                                                                                                                                                        
    message = "UNABLE TO SUBMIT QUERY:  "||message                                                                                                                                                                                                             
    correct_before_exec = 0                                                                                                                                                                                                                                    
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Now format the message for proper display */                                                                                                                                                                                                              
  nextword=""                                                                                                                                                                                                                                                  
  msglines.0=1                                                                                                                                                                                                                                                 
  msgindex=msglines.0                                                                                                                                                                                                                                          
  do index=1 to 2                                                                                                                                                                                                                                              
    msglines.index=""                                                                                                                                                                                                                                          
  end                                                                                                                                                                                                                                                          
  do index=1 to words(message)                                                                                                                                                                                                                                 
    msgindex=msglines.0                                                                                                                                                                                                                                        
    nextword=word(message,index)                                                                                                                                                                                                                               
    /* If we can fit the next word into the line, then do so */                                                                                                                                                                                                
    if (length(msglines.msgindex)+length(nextword)) <= 78 then                                                                                                                                                                                                 
      if length(msglines.msgindex) = 0 then                                                                                                                                                                                                                    
        msglines.msgindex=nextword                                                                                                                                                                                                                             
      else                                                                                                                                                                                                                                                     
        msglines.msgindex=msglines.msgindex||" "||nextword                                                                                                                                                                                                     
    /* Otherwise, pad the rest of the current line, */                                                                                                                                                                                                         
    /* and start a new line.                        */                                                                                                                                                                                                         
    else do                                                                                                                                                                                                                                                    
      msglines.msgindex=left(msglines.msgindex,79)                                                                                                                                                                                                             
      msglines.0=msglines.0+1                                                                                                                                                                                                                                  
      msgindex=msglines.0                                                                                                                                                                                                                                      
      msglines.msgindex=nextword                                                                                                                                                                                                                               
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
  msgtodisplay=""                                                                                                                                                                                                                                              
  do index=1 to msglines.0                                                                                                                                                                                                                                     
    msgtodisplay=msgtodisplay||msglines.index                                                                                                                                                                                                                  
  end                                                                                                                                                                                                                                                          
  address xedit "EMSG" msgtodisplay                                                                                                                                                                                                                            
  message=""                                                                                                                                                                                                                                                   
end                                                                                                                                                                                                                                                            
address xedit "CURSOR SCREEN" err_line err_col                                                                                                                                                                                                                 
                                                                                                                                                                                                                                                               
return                                                                                                                                                                                                                                                         