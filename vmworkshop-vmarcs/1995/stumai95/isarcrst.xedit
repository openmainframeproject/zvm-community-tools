/*********************************************************************/                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* ISARCRST XEDIT                                                    */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Usage:  ISARCRST is an XEDIT-based interactive viewing screen     */                                                                                                                                                                                        
/*         designed to be invoked from the ISARC EXEC.               */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* ISARCRST is an interactive viewing screen which allows the        */                                                                                                                                                                                        
/* customer to view a basic "please wait" type of message while      */                                                                                                                                                                                        
/* awaiting a response from the VMSERV01 service machine (after a    */                                                                                                                                                                                        
/* account has been submitted).                                      */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Exit Code Definitions:                                            */                                                                                                                                                                                        
/*   Exit Codes are not used.                                        */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Required Files:                                                   */                                                                                                                                                                                        
/*   ISARC    EXEC     (Calling program)                             */                                                                                                                                                                                        
/*   MESSAGE  TABLE    (Plain text with mechanism messages)          */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/* Mod Date  Init  Description                                       */                                                                                                                                                                                        
/* --------  ----  ------------------------------------------------- */                                                                                                                                                                                        
/* 07181994  PDL   Initial Development                               */                                                                                                                                                                                        
/* 07291994  PDL   Program/modules names changed.                    */                                                                                                                                                                                        
/* 07291994  PDL   Changed title of screen to "Individual Student    */                                                                                                                                                                                        
/*                 Account Request".                                 */                                                                                                                                                                                        
/* 07291994  PDL   Changed userid display to module name and level   */                                                                                                                                                                                        
/*                 display.                                          */                                                                                                                                                                                        
/* 07291994  PDL   Replaced entire messaging system and modified     */                                                                                                                                                                                        
/*                 component-defined messages to conform.            */                                                                                                                                                                                        
/* 08011994  PDL   Various message text changes.                     */                                                                                                                                                                                        
/* 08051994  PDL   Added fix for message display utility.  The       */                                                                                                                                                                                        
/*                 message area was not properly cleared before      */                                                                                                                                                                                        
/*                 display of another message.                       */                                                                                                                                                                                        
/*                                                                   */                                                                                                                                                                                        
/*********************************************************************/                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
trace off                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
level="106"                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
/* Obtain the record that we are to submit to the service machine. */                                                                                                                                                                                          
parse pull record_to_submit                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
/* Set up the XEDIT environment in a way conducive to */                                                                                                                                                                                                       
/* viewing a message in a very restricted manner.     */                                                                                                                                                                                                       
address xedit                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                               
"SET SCALE    OFF"                                                                                                                                                                                                                                             
"SET PREFIX   OFF"                                                                                                                                                                                                                                             
"SET TOFEOF   OFF"                                                                                                                                                                                                                                             
"SET CTLCHAR % ESCAPE"                                                                                                                                                                                                                                         
"SET CTLCHAR @ PROTECT   HIGH"                                                                                                                                                                                                                                 
"SET CTLCHAR ! PROTECT   NOHIGH"                                                                                                                                                                                                                               
"SET CTLCHAR $ NOPROTECT HIGH"                                                                                                                                                                                                                                 
"SET CTLCHAR ¢ NOPROTECT NOHIGH"                                                                                                                                                                                                                               
"SET MSGLINE  ON 21  2"                                                                                                                                                                                                                                        
"SET CMDLINE  OFF"                                                                                                                                                                                                                                             
"SET MSGMODE  ON"                                                                                                                                                                                                                                              
"SET LINEND   OFF"                                                                                                                                                                                                                                             
"SET CASE     MIXED"                                                                                                                                                                                                                                           
"VERIFY OFF 10 80"                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                               
/* "un-RESERVE" all of the XEDIT lines */                                                                                                                                                                                                                      
/* that were reserved, if any.         */                                                                                                                                                                                                                      
"EXTRACT /RESERVED */"                                                                                                                                                                                                                                         
do index=1 to RESERVED.0                                                                                                                                                                                                                                       
    parse var RESERVED.index linenum otherstuff                                                                                                                                                                                                                
    "SET RESERVED" linenum "OFF"                                                                                                                                                                                                                               
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
/* Default all of the PF keys */                                                                                                                                                                                                                               
do index=1 to 24                                                                                                                                                                                                                                               
    "SET PF"index "BEFORE EMSG You have pressed an invalid PF key. ",                                                                                                                                                                                          
                              "Valid PF keys are shown below."                                                                                                                                                                                                 
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
"SET RESERVED 1 NOH %!ISARCRST ("level")          ",                                                                                                                                                                                                           
                     "%@University of Louisville%!              ",                                                                                                                                                                                             
                     left(date(),11)                                                                                                                                                                                                                           
"SET RESERVED  2 NOH %!                    ",                                                                                                                                                                                                                  
                     "%@Individual Student Account Request%!"                                                                                                                                                                                                  
"SET RESERVED  3 N %@------------------------------------- *",                                                                                                                                                                                                 
                  "--------------------------------------- "                                                                                                                                                                                                   
"SET RESERVED  4 N"                                                                                                                                                                                                                                            
"SET RESERVED -3 N"                                                                                                                                                                                                                                            
"SET RESERVED -2 N %@------------------------------------- *",                                                                                                                                                                                                 
                  "--------------------------------------- "                                                                                                                                                                                                   
                                                                                                                                                                                                                                                               
"CURSOR SCREEN  1 80"                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
address command                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
bypass_transmission=0                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
address xedit "refresh"                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
/* Check to make sure that the account request service machine is up...*/                                                                                                                                                                                      
"PIPE CP QUERY USER VMSERV01 | HOLE"                                                                                                                                                                                                                           
if RC ^= 0 then do                                                                                                                                                                                                                                             
  message.0=3                                                                                                                                                                                                                                                  
  message.1="ERROR RST001: The account request system is currently not"                                                                                                                                                                                        
  message.2="available. Please report this message to the HelpDesk at"                                                                                                                                                                                         
  message.3="502/852-7997."                                                                                                                                                                                                                                    
  call display_message                                                                                                                                                                                                                                         
  bypass_transmission=1                                                                                                                                                                                                                                        
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
if bypass_transmission <> 1 then do                                                                                                                                                                                                                            
  message.0=2                                                                                                                                                                                                                                                  
  message.1="Your account request has been submitted to the system."                                                                                                                                                                                           
  message.2="Please wait for a response."                                                                                                                                                                                                                      
  call display_message                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
  number_of_attempts=1                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
  do loop=1                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
    address xedit "REFRESH"                                                                                                                                                                                                                                    
    "PIPE CP SMSG VMSERV01" record_to_submit "| HOLE"                                                                                                                                                                                                          
    if RC ^= 0 then do                                                                                                                                                                                                                                         
      message.0=3                                                                                                                                                                                                                                              
      message.1="ERROR RST002: The account request system is currently"                                                                                                                                                                                        
      message.2="not accepting requests. Please report this message"                                                                                                                                                                                           
      message.3="to the HelpDesk at 502/852-7997."                                                                                                                                                                                                             
      call display_message                                                                                                                                                                                                                                     
      leave loop                                                                                                                                                                                                                                               
    end                                                                                                                                                                                                                                                        
    "WAKEUP +00:00:20 ( QUIET SMSG"                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
    select                                                                                                                                                                                                                                                     
      when RC=2 then do                                                                                                                                                                                                                                        
        number_of_attempts=number_of_attempts+1                                                                                                                                                                                                                
        if number_of_attempts <> 4 then do                                                                                                                                                                                                                     
          message.0=2                                                                                                                                                                                                                                          
          message.1="The system is responding slowly at this time. Your"                                                                                                                                                                                       
          message.2="request is being re-submitted. Please wait..."                                                                                                                                                                                            
          call display_message                                                                                                                                                                                                                                 
        end                                                                                                                                                                                                                                                    
        else do                                                                                                                                                                                                                                                
          message.0=3                                                                                                                                                                                                                                          
          message.1="ERROR RST003: The system has not responded after"                                                                                                                                                                                         
          message.2="several attempts. Please report this message to"                                                                                                                                                                                          
          message.3="the HelpDesk at 502/852-7997."                                                                                                                                                                                                            
          call display_message                                                                                                                                                                                                                                 
          leave loop                                                                                                                                                                                                                                           
        end                                                                                                                                                                                                                                                    
      end                                                                                                                                                                                                                                                      
      when RC=1 then do                                                                                                                                                                                                                                        
        parse pull flag transmit_id response                                                                                                                                                                                                                   
        response=strip(response)                                                                                                                                                                                                                               
        if flag ^= "*SMSG" then try_again=1                                                                                                                                                                                                                    
        if transmit_id ^= "VMSERV01" then try_again=1                                                                                                                                                                                                          
        if try_again ^= 1 then do                                                                                                                                                                                                                              
          parse var response return_code off_id                                                                                                                                                                                                                
          select                                                                                                                                                                                                                                               
            /* Customer currently has an active account. */                                                                                                                                                                                                    
            when return_code="21" then do                                                                                                                                                                                                                      
              message_index=1                                                                                                                                                                                                                                  
              call retrieve_message_from_table                                                                                                                                                                                                                 
              message.0=message_index-1                                                                                                                                                                                                                        
              call display_message                                                                                                                                                                                                                             
            end                                                                                                                                                                                                                                                
            /* Customer has an account on hold, but */                                                                                                                                                                                                         
            /* will be re-activated.                */                                                                                                                                                                                                         
            when return_code="22" then do                                                                                                                                                                                                                      
              message_index=1                                                                                                                                                                                                                                  
              call retrieve_message_from_table                                                                                                                                                                                                                 
              message.0=message_index-1                                                                                                                                                                                                                        
              call display_message                                                                                                                                                                                                                             
            end                                                                                                                                                                                                                                                
            /* Customer does not have an account, but will have one. */                                                                                                                                                                                        
            when return_code="23" then do                                                                                                                                                                                                                      
              message_index=1                                                                                                                                                                                                                                  
              call retrieve_message_from_table                                                                                                                                                                                                                 
              message.0=message_index-1                                                                                                                                                                                                                        
              call display_message                                                                                                                                                                                                                             
            end                                                                                                                                                                                                                                                
            /* Customer has already requested an account today. */                                                                                                                                                                                             
            when return_code="24" then do                                                                                                                                                                                                                      
              message_index=1                                                                                                                                                                                                                                  
              call retrieve_message_from_table                                                                                                                                                                                                                 
              message.0=message_index-1                                                                                                                                                                                                                        
              call display_message                                                                                                                                                                                                                             
            end                                                                                                                                                                                                                                                
            otherwise                                                                                                                                                                                                                                          
              message.0=4                                                                                                                                                                                                                                      
              message.1="ERROR RST004: The account request system has"                                                                                                                                                                                         
              message.2="responded in an unexpected manner. Please report"                                                                                                                                                                                     
              message.3="this problem to the HelpDesk at 502/852-7997. The"                                                                                                                                                                                    
              message.4="response was" response"."                                                                                                                                                                                                             
              call display_message                                                                                                                                                                                                                             
              leave loop                                                                                                                                                                                                                                       
          end                                                                                                                                                                                                                                                  
          leave loop                                                                                                                                                                                                                                           
        end                                                                                                                                                                                                                                                    
      end                                                                                                                                                                                                                                                      
      otherwise nop                                                                                                                                                                                                                                            
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
address xedit                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                               
"SET PF3  BEFORE QQUIT"                                                                                                                                                                                                                                        
"SET PF12 BEFORE QQUIT"                                                                                                                                                                                                                                        
"SET PF15 BEFORE QQUIT"                                                                                                                                                                                                                                        
"SET PF24 BEFORE QQUIT"                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
"SET RESERVED -1 N %!                              ",                                                                                                                                                                                                          
                  "%@PF3/PF12%! Quit"                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
address command                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
exit                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                               
display_message:                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                               
if message.0 ^= 0 then do                                                                                                                                                                                                                                      
  /* How many messages need to be displayed?  If it is more than */                                                                                                                                                                                            
  /* what we can, display what we can and also report a problem. */                                                                                                                                                                                            
  if (message.0 > 14) then do                                                                                                                                                                                                                                  
    message.0=14                                                                                                                                                                                                                                               
    address xedit "EMSG RST005: Message display overflow, Module",                                                                                                                                                                                             
                  "ISARCRST. Please report this message ",                                                                                                                                                                                                     
                  "to the HelpDesk at 502/852-7997."                                                                                                                                                                                                           
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Now determine the largest length we have in this group of msgs...*/                                                                                                                                                                                       
  largest_length=0                                                                                                                                                                                                                                             
  do find_largest = 1 to (message.0)                                                                                                                                                                                                                           
    if length(message.find_largest) > largest_length then                                                                                                                                                                                                      
      largest_length=length(message.find_largest)                                                                                                                                                                                                              
  end                                                                                                                                                                                                                                                          
  if (largest_length > 78) then do                                                                                                                                                                                                                             
    do trunc_index = 1 to message.0                                                                                                                                                                                                                            
      if (length(message.trunc_index) > 78) then                                                                                                                                                                                                               
        message.trunc_index=left(message.trunc_index,78)                                                                                                                                                                                                       
    end                                                                                                                                                                                                                                                        
    address xedit "EMSG RST006: Message display overflow, Module",                                                                                                                                                                                             
                  "ISARCRST. Please report this message ",                                                                                                                                                                                                     
                  "to the HelpDesk at 502/852-7997."                                                                                                                                                                                                           
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Now determine how much padding we will need */                                                                                                                                                                                                            
  /* on the left to center the body of messages. */                                                                                                                                                                                                            
  left_padding=""                                                                                                                                                                                                                                              
  if largest_length < 78 then                                                                                                                                                                                                                                  
    left_padding=left(left("",78),((78-largest_length)%2))                                                                                                                                                                                                     
                                                                                                                                                                                                                                                               
  /* Now pad the messages, if necessary... */                                                                                                                                                                                                                  
  if length(left_padding) ^= 0 then do                                                                                                                                                                                                                         
    do pad_index=1 to (message.0)                                                                                                                                                                                                                              
      message.pad_index=left_padding||message.pad_index                                                                                                                                                                                                        
    end                                                                                                                                                                                                                                                        
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* First clear all of the possible message lines */                                                                                                                                                                                                          
  address xedit "EXTRACT /RESERVED */"                                                                                                                                                                                                                         
  do display_index=1 to RESERVED.0                                                                                                                                                                                                                             
      parse var RESERVED.display_index linenum otherstuff                                                                                                                                                                                                      
      if (linenum >= 6) & (linenum <= 19) then                                                                                                                                                                                                                 
        address xedit "SET RESERVED" linenum "OFF"                                                                                                                                                                                                             
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  /* Finally display the messages... */                                                                                                                                                                                                                        
  do display_index=6 to (6 + message.0 - 1)                                                                                                                                                                                                                    
    message_ref_no=display_index-6+1                                                                                                                                                                                                                           
    address xedit "SET RESERVED" display_index "NOH",                                                                                                                                                                                                          
                  "%!"message.message_ref_no"%!"                                                                                                                                                                                                               
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
  address xedit "REFRESH"                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
  address xedit "SOS ALARM"                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
  /* Last but not least, clear out our message variables...*/                                                                                                                                                                                                  
  do clear_messages = 1 to (message.0)                                                                                                                                                                                                                         
    message.clear_messages=""                                                                                                                                                                                                                                  
  end                                                                                                                                                                                                                                                          
  message.0=0                                                                                                                                                                                                                                                  
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
retrieve_message_from_table:                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                               
/* Read in the message file, and locate the messages to display */                                                                                                                                                                                             
"PIPE (ENDC ?) < MESSAGE TABLE * |",  /* Read in the message file      */                                                                                                                                                                                      
"     LOCATE 1-9 /"return_code"/ |",  /* Locate the needed messages    */                                                                                                                                                                                      
"     CHANGE /$ACCTID$/"off_id"/ |",  /* Change $ACCTID$ to userid     */                                                                                                                                                                                      
"     STACK FIFO                  "   /* Stack the message lines       */                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
message=""                                                                                                                                                                                                                                                     
do index=1 to queued()                                                                                                                                                                                                                                         
  parse pull . message_line                                                                                                                                                                                                                                    
  message_line=strip(message_line)                                                                                                                                                                                                                             
  message.message_index=message_line                                                                                                                                                                                                                           
  message_index=message_index+1                                                                                                                                                                                                                                
end                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
return                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               