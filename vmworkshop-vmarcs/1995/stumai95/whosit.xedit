/*                                                                   */
/* WHOSIT XEDIT                                                      */
/*                                                                   */
/* Usage:  This file is to be used by either invoking the DOPANEL    */
/*         command and supplying the command with the name of this   */
/*         panel or invoking a XEDIT session on a file with this     */
/*         file specified as the PROFILE to be used.                 */
/*                                                                   */
/* WHOSIT is a XEDIT file containing various XEDIT and REXX          */
/* instructions combined to construct an input panel and the         */
/* instructions necessary to process the input gathered from the     */
/* customer.  See the USAGE statement above for information on how   */
/* to invoke the WHOSIT panel.                                       */
/*                                                                   */
/* Exit Code Definitions:                                            */
/*   Not applicable.                                                 */
/*                                                                   */
/* Mod Date  Init  Description                                       */
/* --------  ----  ------------------------------------------------- */
/* 06291994  PDL   Initial development.                              */
/* 07071994  PDL   Fixed position problem with "KECNET search        */
/*                 not available" item display.                      */
/* 07071994  PDL   Inserted text "WHOIS" into panel title.  Now will */
/*                 read "WHOIS ELECTRONIC DIRECTORY".                */
/* 07211994  PDL   Added message handler.                            */
/* 07211994  PDL   Added code to un-RESERVE XEDIT lines.             */
/* 07211994  PDL   Combined First and Last Name fields into one field*/
/*                 titled "Name".                                    */
/* 07221994  PDL   Removed BETA descriptor from panel.  Release level*/
/*                 106 intended for public release.                  */
/*                                                                   */
 
level="106"
 
/* Define the control characters for panel definition            */
address xedit
"SET CTLCHAR  % ESCAPE"
"SET CTLCHAR  @ PROTECT   HIGH"
"SET CTLCHAR  ! PROTECT   NOHIGH"
"SET CTLCHAR  $ NOPROTECT HIGH"
 
/* Set up the XEDIT environment for panel definition             */
"SET CASE     MIXED"
"SET LINEND   OFF"
"SET TOFEOF   OFF"
"SET PREFIX   OFF"
"SET SCALE    OFF"
"SET MSGLINE  ON 21 2"
"SET CMDLINE  OFF"
"SET MSGMODE  ON"
"VERIFY OFF 1 80"
 
/* Ensure the PF keys are defined as we need them so */
/* that we can avoid the problem with a key defined  */
/* as TABKEY, COPYKEY, NULLKEY, or CP BRKKEY (which  */
/* will cause a XEDIT READ to -NOT- terminate).  We  */
/* cannot have this.                                 */
do index=1 to 24
    "SET PF"index "BEFORE EMSG INTERNAL FAILURE:  EXIT",
    "IMMEDIATELY AND REPORT THIS TO THE HELPDESK (7997)"
end
 
/* Make sure that we set the appropriate PF */
/* keys with a QQUIT definition.  This is to*/
/* make sure the customer can get out even  */
/* though an internal failure has occurred. */
"SET PF3  BEFORE QQUIT"
"SET PF12 BEFORE QQUIT"
"SET PF15 BEFORE QQUIT"
"SET PF24 BEFORE QQUIT"
 
/* "un-RESERVE" all of the XEDIT lines */
/* that were reserved, if any.         */
"EXTRACT /RESERVED */"
do index=1 to RESERVED.0
    parse var RESERVED.index linenum otherstuff
    "SET RESERVED" linenum "OFF"
end
 
address command
 
/* Does the WHOSIT LOCATION file exist?  (This file contains the */
/* KECNET locations that can be queried.)  If not, indicate that */
/* the KECNET search option is not available.                    */
address cms "PIPE CMS STATE WHOSIT LOCATION * | HOLE"
if RC = 28 then locfile="NOTEXIST"
else locfile="EXIST"
 
/* Request that the panel be constructed, and place the cursor   */
/* at the first input field.                                     */
call BuildDisplay
address xedit "CURSOR SCREEN 10 29"
 
/* Initialize all input variables                                */
name=""
/* VARIABLES REMOVED PER 07221994 MODIFICATION */
/* firstname="" */
/* lastname="" */
searchuofl="X"
searchkecn=""
userid=""
node=""
 
/* Begin processing of the panel.  The "status" variable         */
/* indicates what mode we are in.                                */
/*     Status=0    indicates that the customer has submitted the */
/*                 panel for processing.  Syntax/Field checking  */
/*                 is performed as required, and if no problem is*/
/*                 found then the external command is            */
/*                 constructed and executed.                     */
/*     Status=1    indicates that reading of the panel is to     */
/*                 continue until otherwise indicated.           */
/*     Status=2    indicates that an exit from the panel has been*/
/*                 requested.                                    */
/*     Status=3    indicates that help has been requested.       */
status=1
do while (status = 1)
 
    /* Request that the panel be rebuilt to reflect customer entries */
    call BuildDisplay
    /* Request that XEDIT read the panel and return the results      */
    address xedit "READ NOCHANGE NUMBER TAG"
 
    /* Process the results of the XEDIT READ until no more are left  */
    do queued()
        parse pull tag line column data
 
        /* If the result was from an input field, then process it    */
        if tag = "RES" then do
            /* Remove all blanks and underscore characters from the  */
            /* input                                                 */
            data=strip(data,trailing,"_")
            data=strip(data)
            upper data
            select
                /* Line 10 is the name field.                        */
                when (line = 10)
                     then name=data
                /* Line 10, Column 36 is the firstname field         */
                /* when (line = 10) & (column = 36)                  */
                /*      then firstname=data                          */
                /* Line 10, Column 59 is the lastname field          */
                /* when (line = 10) & (column = 59)                  */
                /*      then lastname=data                           */
                /* Line 13 is the search UofL domain selection       */
                when line = 13
                     then do
                              searchuofl=data
                              if searchuofl ^= "" then searchuofl="X"
                          end
                /* Line 14 is the search KECNET domain selection     */
                when line = 14
                     then do
                              searchkecn=data
                              if searchkecn ^= "" then searchkecn="X"
                          end
                /* Line 18 is the user identification field          */
                when line = 18
                     then userid=data
                /* Line 19 is the user node field                    */
                when line = 19
                     then node=data
                otherwise nop
            end
 
        end
 
        /* If the customer pressed a PF key, process it              */
        if tag = "PFK" then do
 
            select
                /* If the customer pressed PF1, PF13, PF9, or PF21   */
                /* then help is requested on the panel.              */
                when (line = 1)  | (line = 13) |,
                     (line = 9)  | (line = 21) then status=3
                /* If the customer pressed PF3, PF15, PF12, or PF24  */
                /* then the customer wishes to exit.                 */
                when (line = 3)  | (line = 15) |,
                     (line = 12) | (line = 24) then do
                    "DESBUF"
                    status=2
                    leave
                end
                /* Otherwise, the PF key is not defined to us.       */
                otherwise
                    message="PFKey is not defined."
                    call display_message
            end
 
        end
 
        /* If the customer pressed the ENTER key, then set the       */
        /* status flag to "check/process input panel".               */
        if tag = "ETK" then status=0
 
    end
 
    /* The customer requested assistance for the panel.              */
    if status = 3 then do
        address command "HELP WHOSIT"
        /* Now, let us resume panel input/processing                 */
        status=1
    end
 
    /* Was panel checking/processing requested?                      */
    if status = 0 then do
 
        /* Check to see if we have any "error" conditions...         */
        select
            /* Scenario 1:  The customer has given no firstname,     */
            /*              lastname, or userid to lookup.           */
            when (userid = "") & (name = "") & (node = "") then do
                status=1
                message="To perform a lookup, you must",
                        "at least specify either a name or a userid."
                call display_message
                address xedit "CURSOR SCREEN 10 29"
            end
            /* Scenario 2:  The customer has requested that both the */
            /*              UofL and KECNET domains be searched.     */
            when (searchuofl ^= "") & (searchkecn ^= "") then do
                status=1
                message="You can only request a search",
                        "in one domain at a time.  Please select",
                        "either the UofL or KECNET",
                        "search domain."
                call display_message
                address xedit "CURSOR SCREEN 13 19"
            end
            /* Scenario 3:  The customer has requested a brief search*/
            /*              but has not indicated a search domain.   */
            when (searchuofl = "") & (searchkecn = "") &,
                 (name ^= "") then do
                status=1
                message="You must specify a search domain",
                        "to perform a search.  Please select",
                        "either the UofL or KECNET search domain."
                call display_message
                address xedit "CURSOR SCREEN 13 19"
            end
            /* Scenario 4:  The customer has indicated the desire for*/
            /*              an extended search by supplying a node,  */
            /*              but a userid is also required.           */
            when (userid = "") & (node ^= "") then do
                status=1
                message="You must specify a userid",
                        "in addition to the node to perform",
                        "an extended search."
                call display_message
                address xedit "CURSOR SCREEN 18 32"
            end
            otherwise nop
        end
 
    end
end
 
/* If status is still zero then we have passed the panel checks.     */
/* Now let us construct and perform the command for the customer.    */
if status = 0 then do
    address cms "CLRSCRN"
    say "Your query has been submitted to the WHOIS server."
    say "Please wait for a response."
    say ""
    say "Your query or queries were:"
 
    /* Was a brief search requested?                                 */
    if (name ^= "") then do
        /* Do we search only the UofL domain?                        */
        if (searchuofl ^= "") then do
            say "     Search the UofL directory for <"name">"
            address cms "WHOIS FIND AT UL" name
        end
        /* Do we search all of KECNET?                               */
        if (searchkecn ^= "") then do
            say "     Search the KECNET directory for <"name">"
            /* Read in the locations to search from WHOSIT LOCATION  */
            address cms "PIPE < WHOSIT LOCATION * |",
                        "     SPEC /WHOIS FIND AT/ 1",
                        "          WORD 1 NEXTWORD |",
                        "     STACK FIFO"
            lastindex=queued()
            do index=1 to lastindex
                parse pull commandtext.index
            end
            do index=1 to lastindex
                address cms commandtext.index name
            end
        end
    end
    /* Did the customer request an extended search?                  */
    if (userid ^= "") then
        if (node = "") then do
            say "     Search for <"userid"> in the directory"
            address cms "WHOIS SHOW" userid
        end
        else do
            say "     Search for <"userid"@"node"> in the directory"
            address cms "WHOIS SHOW" userid"@"node
        end
end
 
address xedit "QQUIT"
exit
 
/* Construct the input panel for the customer...                     */
BuildDisplay:
address xedit
"SET RESERVED  1 N %!WHOSIT                 %@WHOIS ELECTRONIC",
                   "DIRECTORY%!",
                   "                LEVEL"level
"SET RESERVED  4 N %@Enter the information for the person",
                   "you are searching for below.  You may%!"
"SET RESERVED  5 N %@request both types of information by",
                   "completing all fields.%!"
"SET RESERVED  8 N %!     For Brief Information...%!"
"SET RESERVED 10 N %!          Name of Person:",
                   "%$"left(name,30,"_")"%!"
"SET RESERVED 12 N %!          Specify Type of Search (pick one only)%!"
"SET RESERVED 13 N %!              ",
                   "{%$"left(searchuofl,1,"_")"%!} Search only at",
                   "U of L%!"
 
if locfile = "EXIST" then
"SET RESERVED 14 N %!              ",
                   "{%$"left(searchkecn,1,"_")"%!} Search throughout",
                   "the KY Educational Computing Network%!"
else
"SET RESERVED 14 N %!              ",
                   " %!*%!  KECNET Search Not Available%!"
 
"SET RESERVED 16 N %!     For Extended Information...%!"
"SET RESERVED 18 N %!          Specify the Userid",
                   "%$"left(userid,8,"_")"%!"
"SET RESERVED 19 N %!          Specify the Node  ",
                   "%$"left(node,8,"_")"%!"
"SET RESERVED -1 N %@PF1/PF9%! Help     %@PF3/PF12%! Quit"
address command
return
 
display_message:
 
if message ^= "" then do
  /* Now format the message for proper display */
  nextword=""
  msglines.0=1
  msgindex=msglines.0
  do index=1 to 2
    msglines.index=""
  end
  do index=1 to words(message)
    msgindex=msglines.0
    nextword=word(message,index)
    /* If we can fit the next word into the line, then do so */
    if (length(msglines.msgindex)+length(nextword)) <= 78 then
      if length(msglines.msgindex) = 0 then
        msglines.msgindex=nextword
      else
        msglines.msgindex=msglines.msgindex||" "||nextword
    /* Otherwise, pad the rest of the current line, */
    /* and start a new line.                        */
    else do
      msglines.msgindex=left(msglines.msgindex,79)
      msglines.0=msglines.0+1
      msgindex=msglines.0
      msglines.msgindex=nextword
    end
  end
  msgtodisplay=""
  do index=1 to msglines.0
    msgtodisplay=msgtodisplay||msglines.index
  end
  address xedit "EMSG" msgtodisplay
  message=""
end
 
return
